#! Streams and stream combinators.
#!
#! In other languages, streams are often called 'iterators'.

import "value.tao"

class Stream A, e =
    => next : Self -> Maybe (e ~ A, Self)

for A, e member Range A of Stream A e where
    A < Ord + Add A with { Output = A } + One
=
    => next = fn Range (from, to) => if from >= to
        then None
        else
            let one : A = one in # TODO: This line is silly, figure out why we can't infer this
            Just (from, Range (from + one, to))

data Stream S = S

data Mapped S, A, B, f = (A -> f ~ B, S)

for S < Stream A f, A, B, f member Mapped S A B f of Stream B f =
    => next = fn Mapped (f, s) => when s->S.next is
        | None => None
        \ Just (x, s) => Just (effect { x!->f! }, Mapped (f, s))

for S, A, B, f member Stream S of Map A B f where
    S < Stream A f
=
    => Output = Stream (Mapped S A B f)
    => map = fn f, Stream s => Stream Mapped (f, s)

class ToStream A, e where
    Self.Stream < Stream A e
=
    => Stream
    => to_stream : Self -> Self.Stream

for A, e member [e ~ A] of ToStream A e =
    => Stream = [e ~ A]
    => to_stream = fn xs => xs

for S < Stream A e, A, e member Stream S of ToStream A e =
    => Stream = S
    => to_stream = fn Stream s => s

fn to_stream S < ToStream A e, A, e : S -> Stream S.Stream =
    \ s => Stream s->S.to_stream

for A, e member Maybe e ~ A of Stream A e =
    => next = fn
        | None => None
        \ Just x => Just (x, None)

for A, e member [e ~ A] of Stream A e =
    => next = fn
        | [] => None
        \ [x .. xs] => Just (x, xs)

class FromStream A =
    => from_stream : [A] -> Self

for A member [A] of FromStream A =
    => from_stream = fn xs => xs

for A member Maybe A of FromStream A =
    => from_stream = fn
        | [] => None
        \ [x ..] => Just x

member () of FromStream () =
    => from_stream = fn _ => ()

$[util]
fn collect S < Stream A e, A, B < FromStream A, e : Stream S -> e ~ B =
    Stream s => let xs = s->fix(fn rec, s => when s->S.next is
        | None => []
        \ Just (x, s) => [x! .. rec(s)!])! in xs->B.from_stream

# $[util]
# fn collect S < ToStream A e, A, B < FromStream A, e : S -> e ~ B =
#     s => let xs = s->S.to_stream->fix(fn rec, s => when s-><S.Stream>.next is
#         | None => []
#         \ Just (x, s) => [x! .. rec(s)!])! in xs->B.from_stream

$[util]
fn collect_sum S < Stream A e, A, B < Zero + Add A with { Output = B }, e : Stream S -> e ~ B =
    Stream s => s->fix(fn rec, s => when s->S.next is
        | None => B.zero
        \ Just (x, s) => let x = x! in rec(s)! + x)!

$[util]
fn collect_list S < Stream A e, A, e : Stream S -> e ~ [A] =
    Stream s => s->fix(fn rec, s => when s->S.next is
        | None => []
        \ Just (x, s) => [x! .. rec(s)!])!

data Range A = (A, A)

$[util]
fn range A : A -> A -> Stream (Range A) = from, to => Stream Range (from, to)
